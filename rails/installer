#!/usr/bin/env fish
function main



# ## Config
set -l PROG_NAME    "Rails"
set -l REQ_STORAGE  4764
set -l USERNAME     $USER
set -l railsVersion "4.2.1"



# ## Check Requirements
# ## Check if root
if test "$USERNAME" = "root"
  echo "
$PROG_NAME should not be installed from the root user. Please switch
to your normal Koding user, and try this command again.
"
  echo "Req Error: Logged in as root" >&2
  exit 84
end
# ### Check if already installed
if which rails > /dev/null
  echo "
$PROG_NAME is already installed. To create and start a rails app, run
the following commands:

    \$ rails new myApp
    \$ cd myApp
    \$ rails server

You can then visit your app on the following address:

    $USERNAME.koding.io:3000

"
  echo "Req Error: Rails already installed" >&2
  exit 84
end
# ### Check if ruby is installed
if not which ruby2.0 > /dev/null
  echo "
The kpm version of $PROG_NAME requires that Ruby2 is already installed.
Please install it with the following command:

    \$ kpm install ruby2
"
  echo "Req Error: Missing ruby" >&2
  exit 84
end
# ### Storage
set -l available_storage (df / | awk 'NR>1{print $4}')
if test (math $available_storage - $REQ_STORAGE) -lt 0
  echo "
$PROG_NAME requires at least "(math $REQ_STORAGE / 1024)"MB storage, \
and you have "(math $available_storage / 1024)"MB free.

Please free up some space, and try again. You can start by deleting any large
files or directories in your home directory or delete software that you do
not need.

You can also upgrade your account to get more space, at the following url

    https://koding.com/Pricing

Please refer to the following guide for tips on how to free up VM storage:

    http://learn.koding.com/guides/freeing-up-space
"
  echo "Req Error: Not enough storage" >&2
  exit 84
end



# ## Update
sudo apt-get update -y
or begin
  set -l err $status
  echo "Error: Failure updating apt"
  echo "Error: apt-get update failed" >&2
  exit $err
end



# ## Install dependencies
sudo apt-get install -y build-essential libsqlite3-dev \
  libssl-dev zlib1g-dev
or begin
  set -l err $status
  echo "Error: Failure installing dependencies"
  echo "Error: apt-get install failed" >&2
  exit $err
end



# ## Install
echo "Installing rails, this can take a while.."
sudo gem install rails -v "$railsVersion" --no-ri --no-rdoc
or begin
  set -l err $status
  echo "Error: Failure installing rails"
  echo "Error: gem install failed" >&2
  exit $err
end



# ## Add rails alias
sudo cp "/var/lib/gems/2.0.0/gems/railties-$railsVersion/bin/rails" "/usr/local/bin/_rails"
or begin
  set -l err $status
  echo "Error: Failed setting up rails alias"
  echo "Error: Failed setting up rails alias" >&2
  exit $err
end


# ## Add wrapper
sudo curl -L -o "/tmp/railswrapper" "https://raw.githubusercontent.com/koding/kpm-scripts/develop/rails/wrapper"
and sudo mv "/tmp/railswrapper" "/usr/local/bin/rails"
and sudo chmod +x "/usr/local/bin/rails"
or begin
  set -l err $status
  echo "Error: Failed setting up rails wrapper"
  echo "Error: Failed setting up rails wrapper" >&2
  exit $err
end



# ## End
echo "
$PROG_NAME Installation complete! To create and start a rails app, run
the following commands:

    \$ rails new myApp
    \$ cd myApp
    \$ rails server

You can then visit your app on the following address:

    $USERNAME.koding.io:3000

"



end
main
# vim: set filetype=fish:
