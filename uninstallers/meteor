#!/usr/bin/env fish
function main


# ## Config
set -l PROG_NAME    "MeteorJS"
set -l USERNAME     $USER



# ## Check Requirements
# ### User not root
if test "$USERNAME" = "root"
  echo "
kpm installs $PROG_NAME into your home directory. Please switch to your
normal Koding user (not root), and try this command again.
"
  echo "Req Error: Logged in as root" >&2
  exit 1
end
# ### Check if installed
if not test -e "/home/$USERNAME/.meteor"
  echo "
A $PROG_NAME installation cannot be found.
"
  echo "Req Error: Cannot find install" >&2
  exit 1
end



# ## Confirm
echo "
By uninstalling $PROG_NAME, you will DELETE any embedded $PROG_NAME
databases.

This cannot be undone.

Are you sure?
"
for x in (seq 3)
  echo -n "(yes/no, y/n) "
  read -l confirm < /dev/tty
  or begin
    echo "Input Error: Empty confirm response" >&2
    exit 1
  end
  switch "$confirm"
    case "yes" "y"
      break
    case "no" "n"
      exit 1
    case "*"
      if test "$x" -eq 3
        echo -e "\nAborting after 3 failed attempts"
        exit 1
      else
        echo -e "\nInvalid response, please retry."
      end
  end
end



# ## Remove install dir
if test -e "/home/$USERNAME/.meteor"
  rm -rf "/home/$USERNAME/.meteor"
else
  echo "Error: Cannot find meteor install location"
  echo "Error: /home/$USERNAME/.meteor doesn't exist" >&2
  exit 1
end



# ## Remove bin
if test -L "/usr/local/bin/meteor"
  sudo rm "/usr/local/bin/meteor"
else
  # Just going to warn here, since it's not a big deal if we
  # can't find the bin
  echo "Warning: Cannot find meteor binary"
  echo "Warning: Cannot find meteor bin at /usr/local/bin/meteor" >&2
end



echo "
$PROG_NAME has been uninstalled successfully.
"


end
main

# vim: set filetype=fish:
