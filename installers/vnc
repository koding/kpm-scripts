#!/usr/bin/env fish
function main
# ## Config
set -l PROG_NAME    "TightVNC"
set -l REQ_STORAGE  256820
set -l USERNAME     $USER



# ## Check Requirements
# ### Already installed?
if which vncserver > /dev/null
  echo "
$PROG_NAME is already installed. If you need to start it, run the
following command:

  \$ vncserver :1
"
  echo "Req Error: Already installed" >&2
  exit 1
end
# ### Storage
set -l available_storage (df / | awk 'NR>1{print $4}')
if test (math $available_storage - $REQ_STORAGE) -lt 0
  set -l hReq (math $REQ_STORAGE / 1024)
  set -l hAvl (math $available_storage / 1024)
  echo "
$PROG_NAME requires at least $hReq""MB storage, \
and you have $hAvl""MB free.

Please free up some space, and try again. You can start by deleting any
large files or directories in your home directory or delete software that
you do not need.

You can also upgrade your account to get more space, at the following url:

    https://koding.com/Pricing

Please refer to the following guide for tips on how to free up VM storage:

    http://learn.koding.com/guides/freeing-up-space
"
  echo "Req Error: Not enough storage" >&2
  exit 1
end



# ## Get the desired vnc password
stty -echo < /dev/tty
for x in (seq 3)
  echo -n "Enter your desired vnc password: "
  if not read password < /dev/tty
    stty echo < /dev/tty
    echo "Aborting after empty password" >&2
    exit 1
  end
  if test -n $password
    break
  else if test $x -eq 3
    echo -e "\nAborting after 3 empty inputs."
    stty echo < /dev/tty
    echo "Aborting after 3 password attempts" >&2
    exit 1
  else
    echo -e "\nYou must enter a password. Please retry."
  end
end
stty echo < /dev/tty



# This message is here due to a pause on apt-get update that might
# confuse the user. A vague message will help them know that it isn't
# froze.
echo -e "\nUpdating.."



# ## Update
sudo apt-get update
or begin
  set -l err $status
  echo "Error: Failed to update package list"
  echo "Error: apt-get update failed" >&2
  exit $err
end



# ## Install
sudo apt-get install -y xfce4 xfce4-goodies tightvncserver
or begin
  set -l err $status
  echo "Error: Failure installing $PROG_NAME"
  echo "Error: apt-get failed to install $PROG_NAME" >&2
  exit $err
end



# ## Set the password
mkdir -p ~/.vnc
echo "$password" | vncpasswd -f > ~/.vnc/passwd
or begin
  set -l err $status
  echo "Error: Failure to set $PROG_NAME password"
  echo "Error: vncpasswd failed" >&2
  exit $err
end



# ## Chmod passwd
chmod 600 ~/.vnc/passwd
or begin
  set -l err $status
  echo "Error: Failure to set passwd permissions"
  echo "Error: chmod failed" >&2
  exit $err
end



# ## Start vnc
/usr/bin/vncserver :1
or begin
  set -l err $status
  echo "Error: vnc server failed to start"
  echo "Error: vncserver failed to start" >&2
  exit $err
end



# ## Success
echo "
$PROG_NAME installation complete, and the $PROG_NAME server is running.
"
end
main
# vim: set filetype=fish:

