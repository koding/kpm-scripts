#!/usr/bin/env fish
function main

# ## Config
set -l PROG_NAME    "MongoDB"
set -l REQ_STORAGE  108328
set -l USERNAME     $USER


# ## Check Requirements
# ### Storage
set -l available_storage (df / | awk 'NR>1{print $4}')
if test (math $available_storage - $REQ_STORAGE) -lt 0
  echo "
$PROG_NAME requires at least "(math $REQ_STORAGE / 1024)"MB storage, \
and you have "(math $available_storage / 1024)"MB free.

Please free up some space, and try again. You can start by deleting any
large files or directories in your home directory or delete software that
you do not need.

You can also upgrade your account to get more space, at the following url

    https://koding.com/Pricing
"
  echo "Req Error: Not enough storage" >&2
  exit 1
end
# ### Check if already installed
if which ungit > /dev/null
  echo "
$PROG_NAME is already installed. It can be started with the following \
command:

    \$ sudo service mongodb start
"
  echo "Req Error: Already installed" >&2
  exit 1
end



# ## Install
sudo apt-get install mongodb
or begin
  set -l err $status
  echo "Error: Failure installing $PROG_NAME"
  echo "Error: apt-get failed to install $PROG_NAME" >&2
  exit $err
end



# ## Add config
# TODO: Add a check here to see if the config already has the small=true
# line.
echo "smallfiles=true" | sudo tee -a /etc/mongodb.conf
or begin
  set -l err $status
  echo "Error: Failure configuring $PROG_NAME"
  echo "Error: Could not append to mongodb.conf" >&2
  exit $err
end


# ## Restart mongo
sudo service mongodb restart
or begin
  set -l err $status
  echo "Error: Failure restarting $PROG_NAME"
  echo "Error: Failure restarting $PROG_NAME" >&2
  exit $err
end



# ## Complete
echo "
Installation complete, and $PROG_NAME is now running.
"


end
main
# vim: set filetype=fish:
